buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}
plugins {
    id 'eclipse'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.spongepowered.mixin"

mixin {
    add sourceSets.main, "specialmobs.mixins.refmap.json"
}

// The mod's version and id
version = "${mc_version}-${mod_version}"
archivesBaseName = 'specialmobs'
group = "fathertoast.${archivesBaseName}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: "${mc_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                specialmobs {
                    source sourceSets.main
                }
            }
        }

        client {
            property "mixin.debug.export", "true"
            property 'mixin.env.disableRefMap', 'true'

            arg "-mixin.config=specialmobs.mixins.json"
        }

        server {
            property "mixin.debug.export", "true"
            property 'mixin.env.disableRefMap', 'true'

            arg "-mixin.config=specialmobs.mixins.json"
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', 'naturalabsorption'
        }

        data {
            args '--mod', 'specialmobs', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Repos for mods to include during development
    maven {
        name 'CurseMaven'
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    flatDir {
        dir 'flat_deps'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"


    // Crust; only implement the api, but include the entire mod at runtime
    implementation "curse.maven:crust-873570:${crust_api_version}"
    runtimeOnly "curse.maven:crust-873570:${crust_src_version}"

    // Add in JEI and Jade to assist debugging during development
    runtimeOnly fg.deobf("curse.maven:jei-238222:${jei_version}")
    implementation fg.deobf("curse.maven:jade-324717:${jade_version}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "Special Mobs",
                "Specification-Vendor": "FatherToast",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"FatherToast",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "specialmobs.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}